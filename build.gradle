plugins {
    id "java-library"
    id "maven-publish"
}

ext {
    buildNumber = System.getenv("BUILD_NUMBER") ?: System.getenv("TRAVIS_BUILD_NUMBER")
    url = 'https://github.com/tim-group/make-it-easy-templates'
}

group = "com.timgroup"
if (buildNumber) version = "1.0.$buildNumber"

repositories {
    mavenCentral()
}

java {
    withSourcesJar()
    withJavadocJar()
}

dependencies {
    api "com.natpryce:make-it-easy:4.0.1"
    testImplementation "junit:junit:4.12"
    testImplementation "org.hamcrest:hamcrest-library:1.3"
}

tasks.withType(JavaCompile).all {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
    options.encoding = "UTF-8"
    options.incremental = true
    options.deprecation = true
    options.compilerArgs << "-parameters"
}

tasks.withType(Jar).all {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'TIM Group Ltd',
                'Implementation-Vendor-Id': project.group,
                'Implementation-URL': project.url
        )
    }
}

publishing {
    repositories {
        if (project.hasProperty("repoUrl")) {
            maven {
                url = "${project.repoUrl}/repositories/yd-release-candidates"
                credentials {
                    username = project.repoUsername
                    password = project.repoPassword
                }
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId "make-it-easy-templates"
            from components.java
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = DELEGATE_FIRST
                    name project.name
                    description project.description
                    url project.url

                    scm {
                        url "scm:${project.url}"
                        connection 'scm:git:https://github.com/tim-group/make-it-easy-templates.git'
                        developerConnection 'scm:git:git://github.com/tim-group/make-it-easy-templates.git'
                    }

                    developers {
                        developer {
                            email 'opensource@timgroup.com'
                        }
                    }
                }
            }
        }
    }
}
